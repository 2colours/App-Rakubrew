#!/usr/bin/env perl
use strict;
use warnings;
use 5.010;

my $arg = shift // 'help';
my $home = $ENV{HOME};
my $prefix = "$home/.rakudobrew";

my %impls = (
    parrot => {
        configure => 'perl Configure.pl --backends=parrot --gen-parrot --gen-nqp',
        realclean => 'make p-realclean',
    },
    jvm => {
        configure => 'perl Configure.pl --backends=jvm --gen-nqp',
        realclean => 'make j-realclean',
    },
    moar => {
        configure => 'perl Configure.pl --backends=moar --gen-moar --gen-nqp',
        realclean => 'make realclean',
    },
);

sub run {
    system(@_) and die "Failed running ".$_[0]
}

if ($arg eq 'switch') {
    my $impl = shift;
    switch($impl);
    rehash();
} elsif ($arg eq 'rehash') {
    rehash();
} elsif ($arg eq 'list') {
    my $cur = current();
    map { say $cur eq $_ ? "* $_" : "  $_" } list();
} elsif ($arg eq 'current') {
    if (-e "$prefix/CURRENT") {
        say "Currently running " . current()
    } else {
        say "Not running anything at the moment. Use 'rakudobrew switch' to set an implementation";
    }
} elsif ($arg eq 'build') {
    my ($impl, $ver) = (shift, shift);
    if ($impl eq 'all') {
        for (qw(parrot jvm moar)) {
            build_impl($_, $ver);
        }
    } else {
        build_impl($impl, $ver);
    }
} elsif ($arg eq 'build-panda') {
    build_panda();
} else {
    say "Usage:";
    say "rakudobrew current";
    say "rakudobrew list";
    say "rakudobrew build parrot-2013.06";
    say "rakudobrew rehash";
    say "rakudobrew switch [jvm|parrot|moar]";
}

sub current {
    my $cur = slurp("$prefix/CURRENT");
    chomp $cur;
    return $cur;
}

sub list {
    opendir(my $dh, $prefix);
    grep {/^[^.]/ && -d "$prefix/$_" && $_ ne 'bin'} readdir($dh);
}

sub switch {
    my $impl = shift;
    if (!$impl) {
        say "Switch to what?";
        say "Available implementations:";
        map {say} list();
        return;
    }
    opendir(my $dh, $prefix);
    my @match = grep { /$impl/ } list();
    my $ambiguous;
    ($impl, $ambiguous) = @match;
    if (not $ambiguous) {
        say "Switching to $impl";
        spurt("$prefix/CURRENT", $impl);
    } else {
        say "Sorry, I'm not sure if you mean:";
        say $_ for @match;
    }
}

sub build_impl {
    my ($impl, $ver) = @_;
    unless (exists $impls{$impl}) {
        say "Building $impl is NYI. Well volunteered!";
        return;
    }
    $ver //= 'HEAD';
    chdir $prefix;
    unless (-d "$impl-$ver") {
        run "git clone git://github.com/rakudo/rakudo.git $impl-$ver";
    }
    chdir "$impl-$ver";
    run 'git pull';
    run "git checkout $ver";
    if (-e 'Makefile') {
        run $impls{$impl}{realclean};
    }
    run $impls{$impl}{configure};
    run 'make install';
    if (-d 'panda') {
        say "Updating panda as well";
        my $oldcur = current();
        switch("$impl-$ver");
        build_panda();
        switch($oldcur);
    }
    say "Done, $impl-$ver built";
}

sub build_panda {
    my $impl = current();
    chdir "$prefix/$impl";
    unless (-d 'panda') {
        run 'git clone git://github.com/tadzik/panda.git';
    }
    chdir 'panda';
    run 'git pull';
    run 'perl6 rebootstrap.pl';
    say "Done, built panda for $impl";
}

sub slurp {
    my $file = shift;
    open(my $fh, '<', $file);
    local $/ = '';
    my $ret = <$fh>;
    close($fh);
    return $ret;
}

sub spurt {
    my ($file, $cont) = @_;
    open(my $fh, '>', $file);
    say $fh $cont;
    close($fh);
}

sub rehash {
    sub dir {
        my $name = shift;
        opendir(my $dh, $name);
        my @ret;
        while (readdir $dh) {
            next if $_ =~ /^\./;
            push @ret, "$name/$_"
        }
        closedir $dh;
        return @ret;
    }

    sub spurt_shim {
        my ($source, $destdir) = @_;
        my $tmpl = '#!/bin/sh
    %s "$@"
    ';
        my $contents = sprintf $tmpl, $source;
        my ($filename) = $source =~ m{/([^/]+)$};
        spurt("$destdir/$filename", $contents);
        chmod 0755, "$destdir/$filename";
    }

    my $current = current();
    my ($type) = split '-', $current;

    my @paths = "$prefix/$current/install/bin";
    if ($type eq 'parrot') {
        my $parverdir = `$prefix/$current/install/bin/parrot_config versiondir`;
        chomp $parverdir;
        push @paths, "$prefix/$current/install/lib/$parverdir/languages/perl6/site/bin";
    } elsif ($type eq 'jvm') {
        push @paths, "$prefix/$current/install/languages/perl6/site/bin";
    }

    my @bins = map { dir($_) } @paths;
    # TODO: Remove those that were there before?
    for (@bins) {
        spurt_shim $_, "$prefix/bin";
    }
}
